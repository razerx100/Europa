#version 460

#define threadBlockSize 64

layout(local_size_x = threadBlockSize, local_size_y = 1, local_size_z = 1) in;

struct ModelData
{
    mat4  modelMatrix;
    vec4  modelOffset; // materialIndex on the last component.
    uint  meshIndex;
    float modelScale;
    uint  padding[2];
};

struct IndirectArguments
{
    uint indexCount;
    uint instanceCount;
    uint firstIndex;
    int  vertexOffset;
    uint firstInstance;
};

struct CullingData
{
    uint commandCount;
    uint commandOffset;
};

struct Frustum
{
	vec4 left;
	vec4 right;
	vec4 bottom;
	vec4 top;
	vec4 near;
	vec4 far;
};

struct AABB
{
    vec4 maxAxes;
    vec4 minAxes;
};

struct PerModelData
{
    uint modelBundleIndex;
    uint modelIndex;
};

struct PerMeshData
{
    AABB aabb;
};

struct PerMeshBundleData
{
    uint meshOffset;
};

layout(push_constant) uniform Constantdata
{
    uint modelCount;
} constantData;

layout(binding = 0) readonly buffer Modeldata
{
	ModelData models[];
} modelData;

layout(binding = 1) readonly buffer PerModelDataBuffer
{
	PerModelData data[];
} perModelData;

layout(binding = 2) readonly buffer InputArguments
{
    IndirectArguments arguments[];
} inputData;

layout(binding = 3) readonly buffer Cullingdata
{
    CullingData data[];
} cullingData;

layout(binding = 4) buffer OutputArguments
{
    IndirectArguments arguments[];
} outputData;

layout(binding = 5) buffer OutputCounter
{
    uint counters[];
} outputCounters;

layout(binding = 6) readonly buffer PerMeshDataBuffer
{
    PerMeshData data[];
} perMeshData;

layout(binding = 7) readonly buffer PerMeshBundleDataBuffer
{
    PerMeshBundleData data[];
} perMeshBundleData;

layout(binding = 8) readonly buffer MeshBundleIndices
{
    uint indices[];
} meshBundleIndices;

layout(binding = 9) buffer OutputModelIndices
{
    uint indices[];
} outputModelIndices;

layout(binding = 10) uniform CameraMatrices
{
    mat4    view;
    mat4    projection;
    Frustum frustum;
    vec4    viewPosition;
} camera;

bool IsOnOrForwardPlane(vec4 plane, vec3 extents, vec4 centre)
{
    // Collapse the extents on the plane, which would result in a line
    // which should represent the half extent of the AABB.
    float collapsedHalfExtentLine = extents.x * abs(plane.x)
                                + extents.y * abs(plane.y)
                                + extents.z * abs(plane.z);

    // The dot product of the plane and the centre represents the distance of the
    // centre from the plane. If it equals to the collapsedHalfExtent
    // then the centre of the AABB is on the plane and if it is bigger than the
    // collapsedHalfExtent then it is forward on the plane.
    // We are negating the collaspsedHalfExtent because even if the centre dot is
    // negative, the other half of the AABB might still be forward on the plane.
    // It would only be fully outside when it is lower than the negative half extent.
    return -collapsedHalfExtentLine <= dot(plane, centre);
}

bool IsModelInsideFrustum(uint threadIndex)
{
    PerModelData perModelDataInst = perModelData.data[threadIndex];

    uint modelIndex         = perModelDataInst.modelIndex;
    uint modelBundleIndex   = perModelDataInst.modelBundleIndex;
    uint meshBundleIndex    = meshBundleIndices.indices[modelBundleIndex];
    uint meshOffset         = perMeshBundleData.data[meshBundleIndex].meshOffset;
    ModelData modelDataInst = modelData.models[modelIndex];

    vec4 modelOffset = vec4(modelDataInst.modelOffset.xyz, 0.0);
    mat4 world       = modelDataInst.modelMatrix;

    // Local space
    AABB aabb    = perMeshData.data[meshOffset + modelDataInst.meshIndex].aabb;

    vec4 centre  = (aabb.maxAxes + aabb.minAxes) * 0.5;
    vec3 extents = vec3(
        aabb.maxAxes.x - centre.x,
        aabb.maxAxes.y - centre.y,
        aabb.maxAxes.z - centre.z
    );

    // Need to get the x, y and z vectors from the model's world matrix
    // to correctly scale/rotate the extents. And skip the translation.
    // We need to grab them from the rows and in GLSL, the index operator
    // grabs one the of columns.
    vec3 right   = vec3(world[0].x, world[1].x, world[2].x) * extents.x;
    vec3 up      = vec3(world[0].y, world[1].y, world[2].y) * extents.y;
    vec3 forward = vec3(world[0].z, world[1].z, world[2].z) * extents.z;

    // The scaled magnitude of the extents in the world space
    float newX = abs(dot(vec3(1.0, 0.0, 0.0), right))
                + abs(dot(vec3(1.0, 0.0, 0.0), up))
                + abs(dot(vec3(1.0, 0.0, 0.0), forward));

    float newY = abs(dot(vec3(0.0, 1.0, 0.0), right))
                + abs(dot(vec3(0.0, 1.0, 0.0), up))
                + abs(dot(vec3(0.0, 1.0, 0.0), forward));

    float newZ = abs(dot(vec3(0.0, 0.0, 1.0), right))
                + abs(dot(vec3(0.0, 0.0, 1.0), up))
                + abs(dot(vec3(0.0, 0.0, 1.0), forward));

    vec3 scaledExtents     = vec3(newX, newY, newZ);
    // Transform the centre to be in the World space, since the frustum planes are also
    // in the world space.
    vec4 transformedCentre = world * (centre + modelOffset);

    Frustum frustum = camera.frustum;

    return IsOnOrForwardPlane(frustum.left,   scaledExtents, transformedCentre)
        && IsOnOrForwardPlane(frustum.right,  scaledExtents, transformedCentre)
        && IsOnOrForwardPlane(frustum.bottom, scaledExtents, transformedCentre)
        && IsOnOrForwardPlane(frustum.top,    scaledExtents, transformedCentre)
        && IsOnOrForwardPlane(frustum.near,   scaledExtents, transformedCentre)
        && IsOnOrForwardPlane(frustum.far,    scaledExtents, transformedCentre);
}

void main()
{
    uint threadIndex = gl_WorkGroupID.x * threadBlockSize + gl_LocalInvocationIndex;

    if (threadIndex < constantData.modelCount)
    {
        uint modelBundleIndex = perModelData.data[threadIndex].modelBundleIndex;
        CullingData cData     = cullingData.data[modelBundleIndex];

        uint commandEnd       = cData.commandOffset + cData.commandCount;

        // Only process the models which are in the range of the bundle's commands.
        if (cData.commandOffset <= threadIndex && threadIndex < commandEnd)
        {
            if (IsModelInsideFrustum(threadIndex))
            {
                // If the model is inside the frustum, increase the counter by 1.
                // Using the bundle index to index, because each bundle should have its
                // own counter and arguments range.
                uint oldCounterValue = atomicAdd(outputCounters.counters[modelBundleIndex], 1);

                // The argument buffer for this model bundle should start at the commandOffset.
                // Since each argument represent each model, we should put the arguments of the
                // models which are inside the frustum back to back. The old counter value + the
                // offset should be the latest available model index.
                // The reason I am doing it before assigning the argument is because multiple
                // threads could try to write to the same index. But because of the atomicAdd
                // the old value should be unique for this bundle's range. The argument assignment
                // isn't atomic though.
                uint modelWriteIndex                  = cData.commandOffset + oldCounterValue;
                outputData.arguments[modelWriteIndex] = inputData.arguments[threadIndex];

                // Since the models can be culled in any order, there is no way to tell which one
                // was culled from the vertex shader. So, will have write the model indices of
                // the models which weren't culled.
                uint modelIndex                             = perModelData.data[threadIndex].modelIndex;
                outputModelIndices.indices[modelWriteIndex] = modelIndex;
            }
        }
    }
}
