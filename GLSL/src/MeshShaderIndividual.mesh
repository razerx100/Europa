#version 460

#extension GL_EXT_mesh_shader : require

layout(local_size_x = 128, local_size_y = 1, local_size_z = 1) in;
layout(max_vertices = 64, max_primitives = 126) out;
layout(triangles) out;

layout(location = 0) out VetexOutput
{
	vec3 viewVertexPosition;
	vec3 normal;
	vec2 uv;
	uint modelIndex;
	uint materialIndex;
} vOut[];

struct ModelData
{
    mat4 modelMatrix;
    vec4 modelOffset; // materialIndex on the last component.
};

struct Meshlet
{
	uint vertexCount;
    uint vertexOffset;
    uint primitiveCount;
    uint primitiveOffset;
};

struct Vertex
{
    vec3 position;
    vec3 normal;
    vec2 uv;
};

struct VertexOut
{
	vec4 glPosition;
	vec3 viewVertexPosition;
	vec3 normal;
	vec2 uv;
	uint modelIndex;
	uint materialIndex;
};

struct MeshDetails
{
	uint vertexOffset;
	uint vertexIndicesOffset;
	uint primitiveIndicesOffset;
	uint meshletOffset;
};

struct ModelDetails
{
	uint modelIndex;
	uint meshletOffset;
};

layout(push_constant) uniform ConstantData
{
	MeshDetails  meshDetails;
	ModelDetails modelDetails;
} constantData;

layout(binding = 0) readonly buffer Modeldata
{
	ModelData models[];
} modelData;

layout(binding = 1) readonly buffer MeshletData
{
	Meshlet meshlets[];
} meshletData;

layout(binding = 2) readonly buffer VertexData
{
	Vertex vertices[];
} vertexData;

layout(binding = 3) readonly buffer VertexIndexData
{
	uint vertexIndices[];
} vertexIndexData;

layout(binding = 4) readonly buffer PrimitiveIndexData
{
	uint primIndices[];
} primIndexData;

layout(binding = 5) uniform CameraMatrices
{
	mat4 view;
	mat4 projection;
} camera;

uvec3 UnpackPrimitive(uint primitive)
{
	return uvec3(primitive & 0x3FF, (primitive >> 10) & 0x3FF, (primitive >> 20) & 0x3FF);
}

uvec3 GetPrimitive(uint primOffset, uint index)
{
	return UnpackPrimitive(primIndexData.primIndices[primOffset + index]);
}

uint GetVertexIndex(uint vertexIndicesOffset, uint localIndex)
{
	return vertexIndexData.vertexIndices[vertexIndicesOffset + localIndex];
}

VertexOut GetVertexAttributes(
    mat4 modelM, mat4 viewM, mat4 projectionM, mat4 viewNormalM, Vertex vertex,
	vec3 modelOffset, uint modelIndex, uint materialIndex
) {
    mat4 viewSpace          = viewM * modelM;
    vec4 vertexPosition     = vec4(vertex.position + modelOffset, 1.0);
    vec4 viewVertexPosition = viewSpace * vertexPosition;

    VertexOut vout;
    vout.glPosition         = projectionM * viewVertexPosition;
    vout.viewVertexPosition = viewVertexPosition.xyz;
    vout.normal             = mat3(viewNormalM) * vertex.normal;
    vout.uv                 = vertex.uv;
    vout.modelIndex         = modelIndex;
	vout.materialIndex      = materialIndex;

    return vout;
}

VertexOut GetVertexAttributes(uint modelIndex, uint vertexIndex)
{
    Vertex vertex         = vertexData.vertices[vertexIndex];
    const ModelData model = modelData.models[modelIndex];

    vec3 modelOffset      = model.modelOffset.xyz;
    mat4 viewNormalMatrix = transpose(inverse(model.modelMatrix * camera.view));
    uint materialIndex    = floatBitsToUint(model.modelOffset.w);

	return GetVertexAttributes(
		model.modelMatrix, camera.view, camera.projection, viewNormalMatrix, vertex,
		modelOffset, modelIndex, materialIndex
	);
}

void SetOutputs(uint threadIndex, VertexOut pvOut)
{
	vOut[threadIndex].uv                 = pvOut.uv;
	vOut[threadIndex].modelIndex         = pvOut.modelIndex;
	vOut[threadIndex].viewVertexPosition = pvOut.viewVertexPosition;
	vOut[threadIndex].normal             = pvOut.normal;
	vOut[threadIndex].materialIndex      = pvOut.materialIndex;
}

void main()
{
	const uint gid  = gl_WorkGroupID.x;
	const uint gtid = gl_LocalInvocationID.x;

	MeshDetails meshDetails   = constantData.meshDetails;
	ModelDetails modelDetails = constantData.modelDetails;

	uint meshletOffset = meshDetails.meshletOffset + modelDetails.meshletOffset;
	Meshlet meshlet    = meshletData.meshlets[meshletOffset + gid];

	SetMeshOutputsEXT(meshlet.vertexCount, meshlet.primitiveCount);

	if (gtid < meshlet.primitiveCount)
	{
		uint primOffset = meshDetails.primitiveIndicesOffset + meshlet.primitiveOffset;

		gl_PrimitiveTriangleIndicesEXT[gtid] = GetPrimitive(primOffset, gtid);
	}

	if (gtid < meshlet.vertexCount)
	{
		uint vertexIndicesOffset = meshDetails.vertexIndicesOffset + meshlet.vertexOffset;

		VertexOut pvOut = GetVertexAttributes(
			modelDetails.modelIndex, GetVertexIndex(vertexIndicesOffset, gtid)
			);

		gl_MeshVerticesEXT[gtid].gl_Position = pvOut.glPosition;

		SetOutputs(gtid, pvOut);
	}
}
